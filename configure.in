AC_INIT(configure.in)

AM_INIT_AUTOMAKE(balloons, 0.1.8-3)
AC_CONFIG_HEADERS([src/config.h])

dnl AM_GNU_GETTEXT

case $host_os in
  *mingw32* ) MINGW32=yes;;
          * ) MINGW32=no;;
esac

case $host_os in
  *cygwin* ) CYGWIN=yes;;
         * ) CYGWIN=no;;
esac
case $host_os in
  *linux* ) LINUX=yes;;
        * ) LINUX=no;;
esac

case $host_os in
 *darwin* ) MACOSX=yes;;
       * ) MACOSX=no;;
esac

AM_CONDITIONAL(MINGW32, test x$MINGW32 = xyes)
AM_CONDITIONAL(LINUX, test x$LINUX = xyes)

dnl ======================================================================
dnl Check for programs
dnl ======================================================================
CXXTMPFLAGS="$CXXFLAGS"
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_CXX
AC_PROG_CPP
AM_PROG_LIBTOOL
dnl AC_PROG_RANLIB
dnl do not add the AC_PROG_CXX default CXXFLAGS
CXXFLAGS="$CXXTMPFLAGS"


dnl ======================================================================
dnl Check for libraries
dnl ======================================================================

dnl Save a _NOSDL state set of flags for compilation of tools
CFLAGS_NOSDL="$CFLAGS"
CXXFLAGS_NOSDL="$CXXFLAGS"
LIBS_NOSDL="$LIBS"

AM_PATH_SDL(1.2.0)
AC_SUBST(SDL_LIBS)
AC_SUBST(SDL_CFLAGS)
dnl The flags are now substituted inside the Makefile, but not used for the
dnl rest of the script. So we manually put them in. This is necessary if
dnl SDL has not been installed in the canonical locations.

CFLAGS="$CFLAGS $SDL_CFLAGS"
CXXFLAGS="$CXXFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl ----------------------------------------
dnl Check for SDL image library
dnl Check for SDL mixer library
dnl ----------------------------------------
AC_CHECK_LIB(SDL_image,main,,[AC_MSG_ERROR([SDL_image is required to compile BALLOONS])])
AC_CHECK_LIB(SDL_mixer,main,,[AC_MSG_ERROR([SDL_mixer is required to compile BALLOONS])])

SDL_LIBS="$SDL_LIBS -lSDL_image -lSDL_mixer"

AC_CHECK_LIB(SDL_mixer, Mix_QuickLoad_RAW, have_mix_quickload="yes", have_mix_quickload="no")

if test "$have_mix_quickload" = no; then
   AC_MSG_ERROR([SDL_mixer >= 1.2.5 is required to compile BALLOONS])
fi

dnl ----------------------------------------
dnl Check for SDL_ttf library
dnl ---------------------------------------
if test "$MINGW32" = yes; then
  AC_CHECK_LIB(SDL_ttf, main,,[AC_MSG_ERROR([SDL_ttf >= 2.0.6 is required to compile BALLOONS])])
  AC_DEFINE(HAVE_SDLTTF, , [Whether SDL_ttf library was found during configure])
else
  AC_MSG_CHECKING([for SDL_ttf >=2.0.6])
  AC_RUN_IFELSE([AC_LANG_SOURCE(
        [[#include <SDL_ttf.h>
	  int main(int argc, char *argv[]) {
	  if (TTF_MAJOR_VERSION < 2)
            return 1;
          else if (TTF_MAJOR_VERSION == 2 && TTF_MINOR_VERSION == 0 && TTF_PATCHLEVEL < 6) 
            return 1;
          return 0;}]])],
      [AC_MSG_RESULT([found])],
      [AC_MSG_ERROR([SDL_ttf >= 2.0.6 not found.])])
  AC_CHECK_LIB(SDL_ttf, main,,[AC_MSG_ERROR([SDL_ttf is required to compile BALLOONS])])
  AC_DEFINE(HAVE_SDLTTF, , [Whether SDL_ttf library was found during configure])
fi

SDL_LIBS="$SDL_LIBS -lSDL_ttf"

dnl ---------------------------------------
dnl Restoring variables to _NOSDL State
dnl ---------------------------------------

CFLAGS="$CFLAGS_NOSDL"
CXXFLAGS="$CXXFLAGS_NOSDL"
LIBS="$LIBS_NOSDL"


dnl ----------------------------------------------------------
dnl Configure libraries
dnl ----------------------------------------------------------

AC_OUTPUT(
	Makefile
	src/Makefile
	doc/Makefile
	data/Makefile
	data/fonts/Makefile
	data/gfx/Makefile
	data/levels/Makefile
	data/sounds/Makefile
)

AC_MSG_RESULT(
[
   Source directory:    $srcdir
   Installation prefix: $prefix
   C++ compiler:        $CXX $CXXFLAGS
   Libraries:           $LIBS
   Linker options:      $LDFLAGS

if BALLOONS seems to be well configured,
you can now run make ...
]
)
